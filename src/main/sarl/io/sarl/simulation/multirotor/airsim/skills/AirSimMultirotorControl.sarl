/** 
 * 
 */
package io.sarl.simulation.multirotor.airsim.skills

import io.sarl.simulation.multirotor.capacities.MultirotorSimulationControlCapacity
import org.msgpack.rpc.loop.EventLoop
import org.msgpack.rpc.Client
import fr.utbm.airsim.api.MultirotorClientInterface
import fr.utbm.airsim.api.YawMode
import fr.utbm.airsim.api.DrivetrainType

/** 
 * AirSim implementation of the multirotor simulation control capacity
 * @author Alexandre Lombard
 * 
 */
skill AirSimMultirotorControl implements MultirotorSimulationControlCapacity {

	val loop = EventLoop.defaultEventLoop()
	val rpcClient : Client
	val rpcLibClient : MultirotorClientInterface

	// TODO Allow defining this parameter
	val defaultTimeout = 5.0f

	new() {
		this("127.0.0.1", 41451)
	}

	new(ipAddress : String, port : Integer) {
		rpcClient = new Client(ipAddress, port, loop)
		rpcLibClient = rpcClient.proxy(typeof(MultirotorClientInterface))
	}
	
	override enableControl(vehicleName : String) {
		synchronized (this.rpcLibClient) {
			rpcLibClient.enableApiControl(true, vehicleName)
		}
	}

	override takeOff(vehicleName : String) {
		synchronized (this.rpcLibClient) {
			rpcLibClient.takeoffAsync(defaultTimeout, vehicleName)	
		}
	}

	override land(vehicleName : String) {
		synchronized (this.rpcLibClient) {
			rpcLibClient.landAsync(defaultTimeout, vehicleName)		
		}
	}

	override rotateByYawRate(vehicleName : String, yawRate : double, duration : double) {
		synchronized (this.rpcLibClient) {
			rpcLibClient.rotateByYawRateAsync(yawRate as float, duration as float, vehicleName)	
		}
	}

	override rotateToYaw(vehicleName : String, yaw : double) {
		synchronized (this.rpcLibClient) {
			rpcLibClient.rotateToYawAsync(yaw as float, defaultTimeout, 5.0f, vehicleName)	
		}
	}

	override goHome(vehicleName : String) {
		synchronized (this.rpcLibClient) {
			rpcLibClient.goHomeAsync(defaultTimeout, vehicleName)	
		}
	}

	override moveToPosition(vehicleName : String, x : double, y : double, z : double, velocity : double) {
		synchronized (this.rpcLibClient) {
			rpcLibClient.moveToPositionAsync(x as float, y as float, z as float, velocity as float, defaultTimeout,
				DrivetrainType.MAX_DEGREE_OF_FREEDOM,
				new YawMode(), -1.0f, -1.0f, vehicleName)	
		}
	}

	override moveByVelocity(vehicleName : String, vx : double, vy : double, vz : double, duration : double) {
		synchronized (this.rpcLibClient) {
			rpcLibClient.moveByVelocityAsync(vx as float, vy as float, vz as float, duration as float,
				DrivetrainType.MAX_DEGREE_OF_FREEDOM, new YawMode(),
				vehicleName)
		}
	}
}
